# TensorFlow Lite for iOS

load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@build_bazel_rules_apple//apple:ios.bzl", "ios_static_framework")
load("//machina:pytype.default.bzl", "pytype_strict_binary", "pytype_strict_library")
load("//machina:strict.default.bzl", "py_strict_test")
load(
    "//machina/lite/ios:ios.bzl",
    "TFL_MINIMUM_OS_VERSION",
    "strip_common_include_path_prefix",
    "tflite_ios_framework",
    "tflite_ios_xcframework",
)

package(
    default_visibility = [
        "//machina/lite:__subpackages__",
    ],
    licenses = ["notice"],
)

sh_binary(
    name = "hide_symbols_with_allowlist",
    srcs = [
        "hide_symbols_with_allowlist.sh",
    ],
    visibility = [
        "//machina/lite:__subpackages__",
        "@org_machina_lite_support//machina_lite_support:__subpackages__",
    ],
)

sh_binary(
    name = "hide_xcframework_symbols_with_allowlist",
    srcs = [
        "hide_xcframework_symbols_with_allowlist.sh",
    ],
    visibility = [
        "//machina/lite:__subpackages__",
        "@org_machina_lite_support//machina_lite_support:__subpackages__",
    ],
)

pytype_strict_library(
    name = "extract_object_files",
    srcs = [
        "extract_object_files.py",
    ],
)

pytype_strict_binary(
    name = "extract_object_files_main",
    srcs = [
        "extract_object_files_main.py",
    ],
    python_version = "PY3",
    srcs_version = "PY3",
    visibility = [
        "//machina/lite:__subpackages__",
        "@org_machina_lite_support//machina_lite_support:__subpackages__",
    ],
    deps = [
        ":extract_object_files",
    ],
)

filegroup(
    name = "extract_object_files_testdata",
    srcs = glob(["testdata/**"]),
)

py_strict_test(
    name = "extract_object_files_test",
    srcs = [
        "extract_object_files_test.py",
    ],
    data = [
        ":extract_object_files_testdata",
    ],
    python_version = "PY3",
    srcs_version = "PY3",
    deps = [
        ":extract_object_files",
        "//machina/python/platform:client_testlib",
        "//machina/python/platform:resource_loader",
        "@absl_py//absl/testing:parameterized",
    ],
)

strip_common_include_path_prefix(
    name = "strip_common_include_path_core",
    hdr_labels = [
        "//machina/lite:builtin_ops.h",
        # Here we include the paths of the actual c library headers in machina/lite/core/c instead of the shim headers in machina/lite/c.
        "//machina/lite/core/c:c_api.h",
        "//machina/lite/core/c:c_api_experimental.h",
        "//machina/lite/core/c:c_api_opaque.h",
        "//machina/lite/core/c:c_api_types.h",
        "//machina/compiler/mlir/lite/core/c:tflite_types.h",
        "//machina/lite/core/c:common.h",
        "//machina/lite/core/c:operator.h",
        "//machina/lite/core/async/c:types.h",
        "//machina/lite/delegates/xnnpack:xnnpack_delegate.h",
        "//machina/lite/profiling/telemetry/c:profiler.h",
        "//machina/lite/profiling/telemetry/c:telemetry_setting.h",
    ],
)

strip_common_include_path_prefix(
    name = "strip_common_include_path_subspecs",
    hdr_labels = [
        "//machina/lite/delegates/coreml:coreml_delegate.h",
        "//machina/lite/delegates/gpu:metal_delegate.h",
    ],
    prefix = "TensorFlowLiteC/",
)

# bazel build -c opt --config=ios_fat //machina/lite/ios:TensorFlowLiteC_framework
tflite_ios_framework(
    name = "TensorFlowLiteC_framework",
    hdrs = [
        ":builtin_ops.h",
        ":c_api.h",
        ":c_api_experimental.h",
        ":c_api_opaque.h",
        ":c_api_types.h",
        ":common.h",
        ":operator.h",
        ":profiler.h",
        ":telemetry_setting.h",
        ":tflite_types.h",
        ":types.h",
        ":xnnpack_delegate.h",
    ],
    allowlist_symbols_file = ":allowlist_TensorFlowLiteC.txt",
    bundle_name = "TensorFlowLiteC",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        ":machina_lite_c",
    ],
)

# bazel build -c opt --config=ios //machina/lite/ios:TensorFlowLiteC_xcframework
tflite_ios_xcframework(
    name = "TensorFlowLiteC_xcframework",
    allowlist_symbols_file = ":allowlist_TensorFlowLiteC.txt",
    bundle_name = "TensorFlowLiteC",
    ios = {
        "simulator": [
            "x86_64",
            "arm64",
        ],
        "device": ["arm64"],
    },
    minimum_os_versions = {"ios": TFL_MINIMUM_OS_VERSION},
    public_hdrs = [
        ":builtin_ops.h",
        ":c_api.h",
        ":c_api_experimental.h",
        ":c_api_types.h",
        ":common.h",
        ":profiler.h",
        ":telemetry_setting.h",
        ":xnnpack_delegate.h",
    ],
    tags = ["manual"],  # TODO: Remove once tf is on bazel 6.x+
    deps = [
        ":machina_lite_c",
    ],
)

# Similar to TensorFlowLiteC_framework but this is a static framework and symbol
# hiding is not applied. Note both have the same bundle name.
ios_static_framework(
    name = "TensorFlowLiteC_static_framework",
    hdrs = [
        ":builtin_ops.h",
        ":c_api.h",
        ":c_api_experimental.h",
        ":c_api_opaque.h",
        ":c_api_types.h",
        ":common.h",
        ":operator.h",
        ":profiler.h",
        ":telemetry_setting.h",
        ":tflite_types.h",
        ":types.h",
        ":xnnpack_delegate.h",
    ],
    bundle_name = "TensorFlowLiteC",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        ":machina_lite_c",
    ],
)

# This target builds the flex delegate as a separate static framework, which
# does not include the TensorFlow Lite runtime. As this target does not contain
# TensorFlow Lite runtime, it is intended to be linked along with the
# TensorFlowLiteC framework above in a composable way.
#
# The flex delegate cannot be built for i386, so it can't be built with ios_fat
# config.
#
# bazel build -c opt --config=ios --ios_multi_cpus=armv7,arm64,x86_64 //machina/lite/ios:TensorFlowLiteSelectTfOps_framework
ios_static_framework(
    name = "TensorFlowLiteSelectTfOps_framework",
    avoid_deps = [
        "//machina/lite/core/c:common",
        "//machina/lite/core/async/interop/c:types",
        "//machina/lite/profiling/telemetry/c:telemetry_setting",
    ],
    bundle_name = "TensorFlowLiteSelectTfOps",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        "//machina/lite/delegates/flex:delegate",
    ],
)

# This target builds the Core ML delegate as a separate static framework, which
# does not include the TensorFlow Lite runtime. As this target does not contain
# TensorFlow Lite runtime, it is intended to be linked along with the
# TensorFlowLiteC framework above in a composable way.
#
# bazel build -c opt --config=ios_fat //machina/lite/ios:TensorFlowLiteCCoreML_framework
tflite_ios_framework(
    name = "TensorFlowLiteCCoreML_framework",
    hdrs = [
        ":coreml_delegate.h",
    ],
    allowlist_symbols_file = ":allowlist_TensorFlowLiteCCoreML.txt",
    bundle_name = "TensorFlowLiteCCoreML",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        "//machina/lite/delegates/coreml:coreml_delegate",
    ],
)

# This target builds the Metal delegate as a separate static framework, which
# does not include the TensorFlow Lite runtime. As this target does not contain
# TensorFlow Lite runtime, it is intended to be linked along with the
# TensorFlowLiteC framework above in a composable way.
#
# bazel build -c opt --config=ios_fat //machina/lite/ios:TensorFlowLiteCMetal_framework
tflite_ios_framework(
    name = "TensorFlowLiteCMetal_framework",
    hdrs = [
        ":metal_delegate.h",
    ],
    allowlist_symbols_file = ":allowlist_TensorFlowLiteCMetal.txt",
    bundle_name = "TensorFlowLiteCMetal",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        "//machina/lite/delegates/gpu:metal_delegate",
    ],
)

cc_library(
    name = "machina_lite_c",
    hdrs = [
        "//machina/compiler/mlir/lite/core/c:tflite_types.h",
        "//machina/lite:builtin_ops.h",
        "//machina/lite/core/async/c:types.h",
        "//machina/lite/core/c:c_api.h",
        "//machina/lite/core/c:c_api_experimental.h",
        "//machina/lite/core/c:c_api_opaque.h",
        "//machina/lite/core/c:c_api_types.h",
        "//machina/lite/core/c:common.h",
        "//machina/lite/core/c:operator.h",
        "//machina/lite/delegates/xnnpack:xnnpack_delegate.h",
        "//machina/lite/profiling/telemetry/c:profiler.h",
        "//machina/lite/profiling/telemetry/c:telemetry_setting.h",
    ],
    tags = [
        "nobuilder",
        "swift_module=TensorFlowLiteC",
    ],
    deps = [
        "//machina/compiler/mlir/lite/core/c:tflite_types",
        "//machina/lite:builtin_ops",
        "//machina/lite/core/async/c:types",
        "//machina/lite/core/c:c_api",
        "//machina/lite/core/c:c_api_experimental",
        "//machina/lite/core/c:c_api_types",
        "//machina/lite/core/c:common",
        "//machina/lite/delegates/xnnpack:xnnpack_delegate",
        "//machina/lite/profiling/telemetry/c:profiler",
        "//machina/lite/profiling/telemetry/c:telemetry_setting",
    ],
)

# Used for building TensorFlowLiteC framework.
build_test(
    name = "framework_build_test",
    # build_test targets are not meant to be run with sanitizers.
    tags = [
        "noasan",
        "nomsan",
        "notsan",
        # TODO(b/176993122): restore once the apple_genrule issue is resolved.
        "notap",
        "nozapfhahn",
    ],
    targets = [
        ":TensorFlowLiteCCoreML_framework",
        ":TensorFlowLiteCMetal_framework",
        ":TensorFlowLiteC_framework",
        ":TensorFlowLiteSelectTfOps_framework",
        ":TensorFlowLiteC_static_framework",
    ],
)
